use { Super.{Map, False.Not} }

// Equality
inductive Eq(A: Type, x: A): (y: A) → Type {
    refl: Eq A x x,
}

// Congruence of equality: any function may be applied to both sides of an equation
def Eq.congrue(A B: Type, f: A → B, x y: A, p: Eq A x y): Eq B (f x) (f y) {
    match p to [y -] Eq B (f x) (f y) {
        refl => Eq.refl B (f x)
    }
}

// Transitivity of equality: if x=y and y=z, then x=z
def Eq.compose(A: Type, x y z: A, p: Eq A x y, q: Eq A y z): Eq A x z {
    match q to [z -] Eq A x z {
        refl => p,
    }
}

// Symmetry of equality: if x=y, then y=x
def Eq.commute(A: Type, x y: A, p: Eq A x y): Eq A y x {
    match p to [y -] Eq A y x {
        refl => Eq.refl A x,
    }
}

// HoTT Lemma 2.3.1
def Eq.transport(A: Type, B: A → Type, x y: A, p: Eq A x y): (B x) → B y {
    match p to [y -] (B x) → B y {
        refl => Map.id B x,
    }
}

def Neq(A: Type, x y: A): Type { Not (Eq A x y) }
